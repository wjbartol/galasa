plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
}

def distRepo = System.getenv('MAVEN_REPO') ?: "https://nexus.galasa.dev/repository/maven-development"

group   = "dev.galasa"
version = "0.15.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.felix:org.apache.felix.bundlerepository:2.0.10'
    implementation 'org.reflections:reflections:0.9.11'
    implementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'junit:junit:4.13'
}

gradlePlugin {
    // Define the plugins
    plugins {
        obr {
            id = 'dev.galasa.obr'
            implementationClass = 'dev.galasa.gradle.obr.ObrPlugin'
        }
        tests {
            id = 'dev.galasa.tests'
            implementationClass = 'dev.galasa.gradle.tests.TestsPlugin'
        }
        githash {
            id = 'dev.galasa.githash'
            implementationClass = 'dev.galasa.gradle.hashes.GitHashPlugin'
        }
        testcatalog {
            id = 'dev.galasa.testcatalog'
            implementationClass = 'dev.galasa.gradle.testcatalog.TestCatalogPlugin'
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
        }
    }
    repositories {
        maven {
            name = 'distRepo'
            url  = distRepo
            
            credentials {
                username System.getenv('MAVENUSERNAME')
                password System.getenv('MAVENPASSWORD')
            }
        }
    }
}

