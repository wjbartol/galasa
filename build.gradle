plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
}

group   = "dev.galasa"
version = "0.15.0"

repositories {
    maven {
       url "$sourceMaven"
    }
    maven {
       url "$centralMaven"
    }
}

dependencies {
    implementation 'org.apache.felix:org.apache.felix.bundlerepository:2.0.10'
    implementation 'org.reflections:reflections:0.9.11'
    implementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'junit:junit:4.13'
}

gradlePlugin {
    // Define the plugins
    plugins {
        obr {
            id = 'dev.galasa.obr'
            implementationClass = 'dev.galasa.gradle.obr.ObrPlugin'
        }
        tests {
            id = 'dev.galasa.tests'
            implementationClass = 'dev.galasa.gradle.tests.TestsPlugin'
        }
        githash {
            id = 'dev.galasa.githash'
            implementationClass = 'dev.galasa.gradle.hashes.GitHashPlugin'
        }
        testcatalog {
            id = 'dev.galasa.testcatalog'
            implementationClass = 'dev.galasa.gradle.testcatalog.TestCatalogPlugin'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}
    
tasks.withType(Javadoc) {
    failOnError = false
    enabled = true
}


signing {
    sign publishing.publications
}



tasks.withType(Sign) {
    onlyIf { isRelease.toBoolean() }
}



publishing {
    afterEvaluate {
        publications {
            pluginMaven(MavenPublication) {
            }

            withType(MavenPublication) {
                pom {
                    url = 'https://galasa.dev'
                    name = 'Galasa Gradle Plugins'
                    description = 'This project contains the Galasa Gradle plugins'
                    licenses {
                        license {
                            name = 'Eclipse Public License - v 2.0'
                            url = 'https://www.eclipse.org/legal/epl-2.0/t'
                        }
                    }
                    developers {
                        developer {
                            name = 'Michael Baylis'
                            email = 'Michael.Baylis@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'William Yates'
                            email = 'wyates@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Michael Cobbett'
                            email = 'mike_cobbett@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Jade Carino'
                            email = 'jade.carino@ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Savvas Kyriacou'
                            email = 'savvasky@uk.ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Aaron Jhaj'
                            email = 'aaron.jhaj@ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                        developer {
                            name = 'Eamonn Mansour'
                            email = 'eamonn.mansour@ibm.com'
                            organization = 'IBM'
                            organizationUrl = 'https://www.ibm.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git:://github.com/galasa-dev/gradle'
                        developerConnection = 'scm:git:git:://github.com/galasa-dev/gradle'
                        url = 'https://github.com/galasa-dev/gradle'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/galasa-dev/projectmanagement/issues'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url  = "$targetMaven"
            
            if ("$targetMaven".startsWith('http')) {
                credentials {
                    username System.getenv('MAVENUSERNAME')
                    password System.getenv('MAVENPASSWORD')
                }
            }
        }
    }
}

