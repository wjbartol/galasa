plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenLocal()
    maven {
       url "$sourceMaven"
    }
    maven {
       url "$centralMaven"
    }
}

dependencies {
    implementation platform('dev.galasa:dev.galasa.platform:0.39.0')
    implementation 'org.apache.felix:org.apache.felix.bundlerepository'
    implementation 'org.reflections:reflections'
    implementation 'com.google.code.gson:gson'
    testImplementation 'junit:junit'
    implementation project(':dev.galasa.plugin.common')
    implementation project(':dev.galasa.plugin.common.impl')
    testImplementation  project(':dev.galasa.plugin.common.test')
}

tasks.withType(Javadoc) {
    failOnError = false
    enabled = true
}

task gitHash(dependsOn : 'classes') {
    def dir    = layout.buildDirectory.dir("githash").get().asFile
    def meta   = layout.buildDirectory.dir("githash/META-INF").get().asFile
    def result = layout.buildDirectory.file("githash/META-INF/git.hash").get().asFile
    
    outputs.dir meta
    tasks.jar.dependsOn('gitHash')
    tasks.jar.from(dir)
    
    doLast {
        if(!meta.exists()) {
            meta.mkdirs()
        }
        
        def hash = "unknown"
        
        try {
            new ByteArrayOutputStream().withStream { os ->
                def r = exec {
                    commandLine 'git', 'log',  '--pretty=format:%H', '-1', '.'
                    standardOutput = os
                }
            hash = os.toString()
            }
        } catch(Exception e) {}
        result.write(hash)
    }
}

gradlePlugin {
    // Define the plugins
    plugins {
        obr {
            id = 'dev.galasa.obr'
            implementationClass = 'dev.galasa.gradle.obr.ObrPlugin'
        }
        tests {
            id = 'dev.galasa.tests'
            implementationClass = 'dev.galasa.gradle.tests.TestsPlugin'
        }
        githash {
            id = 'dev.galasa.githash'
            implementationClass = 'dev.galasa.gradle.hashes.GitHashPlugin'
        }
        testcatalog {
            id = 'dev.galasa.testcatalog'
            implementationClass = 'dev.galasa.gradle.testcatalog.TestCatalogPlugin'
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}
    
tasks.withType(Javadoc) {
    failOnError = false
    enabled = true
}


signing {
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    sign publishing.publications
}



tasks.withType(Sign) {
    onlyIf { isMainOrRelease.toBoolean() }
}



publishing {
    afterEvaluate {
        publications {
            pluginMaven(MavenPublication) {
            }

            withType(MavenPublication) {
                pom {
                    url = 'https://galasa.dev'
                    name = 'Galasa Gradle Plugins'
                    description = 'This project contains the Galasa Gradle plugins'
                    licenses {
                        license {
                            name = 'Eclipse Public License - v 2.0'
                            url = 'https://www.eclipse.org/legal/epl-2.0'
                        }
                    }
                    developers {
                        developer {
                            name = 'Galasa Contributors'
                            email = 'galasadelivery@ibm.com'
                            organization = 'Linux Foundation'
                            organizationUrl = 'https://github.com/galasa-dev'
                        }
                    }
                    scm {
                        connection = 'scm:git:git:://github.com/galasa-dev/galasa'
                        developerConnection = 'scm:git:git:://github.com/galasa-dev/galasa'
                        url = 'https://github.com/galasa-dev/galasa'
                    }
                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/galasa-dev/projectmanagement/issues'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url  = "$targetMaven"
            
            if ("$targetMaven".startsWith('http')) {
                credentials {
                    username System.getenv("GITHUB_ACTOR")
                    password System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}

